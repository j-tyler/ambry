// Copyright (C) 2014-2016 LinkedIn Corp. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License"); you may not use
// this file except in compliance with the License. You may obtain a copy of the
// License at  http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied.

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

apply plugin: 'org.shipkit.shipkit-auto-version'
println "Building version $version"

apply from: file('gradle/license.gradle')
apply from: file('gradle/environment.gradle')
apply from: file("gradle/dependency-versions.gradle")
if (!project.hasProperty('disableShipkit')) {
    apply from: file("gradle/ci-release.gradle")
}

allprojects {
    group = "com.github.ambry"

    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'project-report'
    apply plugin: 'jacoco'
    apply plugin: 'org.gradle.test-retry'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    configurations.all {
        resolutionStrategy {
            // Force avro-parent to version 1.11.4. Currently, a vulnerable version 1.7.7 is being picked up transitively.
            force 'org.apache.avro:avro-parent:1.11.4'
            // Force protobuf-parent to version 3.15.0. Currently, a vulnerable version 3.11.4 is being picked up
            // transitively.
            force 'com.google.protobuf:protobuf-parent:3.15.0'
            // Keep ALL Jackson modules aligned; prevents transitive bumps (e.g., to 2.13.5)
            force "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion",
                    "com.fasterxml.jackson.core:jackson-core:$jacksonVersion",
                    "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
                    "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jacksonVersion",
                    "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion",
                    "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion",
                    "com.fasterxml.jackson.module:jackson-module-afterburner:$jacksonVersion"
        }
    }
}

// ByteBuf Flow Tracker agent build tasks (root level)
// These tasks are defined once at the root level and referenced by all subproject test tasks
// Enable with: ./gradlew test -PwithByteBufTracking

// Task to initialize and update the ByteBuf tracker git submodule
// Always pulls the latest from upstream when -PwithByteBufTracking is used
task initByteBufTrackerSubmodule(type: Exec) {
    description = 'Initializes and updates the ByteBuf Flow Tracker git submodule to latest upstream'
    group = 'build'

    def submoduleDir = file("${rootProject.projectDir}/modules/bytebuddy-bytebuf-tracer")

    workingDir rootProject.projectDir
    commandLine 'git', 'submodule', 'update', '--init', '--remote', '--merge', 'modules/bytebuddy-bytebuf-tracer'

    doFirst {
        logger.lifecycle("Updating ByteBuf tracker submodule to latest upstream...")
    }
}

// Task to build the ByteBuf tracker agent from the git submodule
// This runs once before any tests, not once per subproject
task buildByteBufAgent(type: Exec) {
    description = 'Builds the ByteBuf Flow Tracker agent from the git submodule using Maven'
    group = 'build'

    dependsOn initByteBufTrackerSubmodule

    def submoduleDir = file("${rootProject.projectDir}/modules/bytebuddy-bytebuf-tracer")
    def agentJar = file("${submoduleDir}/bytebuf-flow-tracker/target/bytebuf-flow-tracker-1.0.0-SNAPSHOT-agent.jar")

    workingDir submoduleDir
    commandLine 'mvn', 'clean', 'package', '-DskipTests'

    doFirst {
        // Check if Maven is available
        try {
            def mvnCheck = new ProcessBuilder('mvn', '--version').start()
            mvnCheck.waitFor()
            if (mvnCheck.exitValue() != 0) {
                throw new Exception("Maven command failed")
            }
        } catch (Exception e) {
            throw new GradleException(
                "Maven is required to build the ByteBuf tracker agent but was not found.\n\n" +
                "Please install Maven:\n" +
                "  macOS:    brew install maven\n" +
                "  Ubuntu:   sudo apt-get install maven\n" +
                "  Windows:  choco install maven\n" +
                "  Other:    https://maven.apache.org/download.cgi\n\n" +
                "Or run tests without ByteBuf tracking:\n" +
                "  ./gradlew test"
            )
        }
    }

    // Only run if the submodule exists (after init)
    onlyIf {
        if (!submoduleDir.exists() || !file("${submoduleDir}/bytebuf-flow-tracker/pom.xml").exists()) {
            throw new GradleException(
                "ByteBuf tracker submodule not initialized at: ${submoduleDir}\n\n" +
                "This should have been initialized automatically, but failed.\n" +
                "Try manually running:\n" +
                "  git submodule init\n" +
                "  git submodule update"
            )
        }
        true
    }

    // Skip if the JAR is already up-to-date (simple optimization)
    outputs.file(agentJar)
    // Only set inputs if the directory exists to avoid configuration-time errors
    def srcDir = file("${submoduleDir}/bytebuf-flow-tracker/src")
    if (srcDir.exists()) {
        inputs.dir(srcDir)
    }
}

subprojects {
    apply plugin: 'java'

    if (!project.hasProperty('disableShipkit')) {
        apply from: "$rootDir/gradle/java-publishing.gradle"
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    if (JavaVersion.current().java9Compatible) {
        // Our consumers still run on java 8, so this flag ensures that our builds are backwards compatible at runtime
        // with java 8 platform APIs.
        compileJava {
            options.compilerArgs.addAll(['--release', '8'])
        }
    }

    sourceSets {
        // separate source set for integration tests
        intTest {
            java.srcDir file('src/integration-test/java')
            resources.srcDir file('src/integration-test/resources')
            compileClasspath += sourceSets.main.output + sourceSets.test.output
            runtimeClasspath += sourceSets.main.output + sourceSets.test.output
        }
    }

    configurations {
        // This configuration allows for test suites to properly depend on utility classes in other subprojects.
        // Dependencies should be included in the classpath when depending on testArtifacts in another subproject
        testArchives.extendsFrom testRuntimeOnly

        // These configurations allow for the integration test source set to be compiled and run as a test suite.
        // Integration tests should be able to get the same dependencies as the corresponding unit tests.
        intTestImplementation.extendsFrom testImplementation
        intTestRuntimeOnly.extendsFrom testRuntimeOnly
    }

    // this test jar is used to represent a test dependency for a subproject, since depending directly on a source set
    // does not guarantee that the source set was actually built.
    task testJar(type: Jar, dependsOn: testClasses) {
        archiveClassifier.set('test')
        from sourceSets.test.output
    }

    artifacts {
        // Depending on a jar instead of the source set directly prevents test subprojects from compiling before their
        // dependencies are built.
        testArchives testJar
    }

    dependencies {
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        implementation "com.google.code.findbugs:jsr305:$jsr305Version"
        testImplementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
        testImplementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
        testImplementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
        testImplementation "junit:junit:$junitVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
        testRuntimeOnly project(':log4j-test-config')
    }

    idea {
        // since integration tests are in a separate source set, we have to tell intellij about them
        module {
            testSourceDirs += sourceSets.intTest.java.srcDirs
            testResourceDirs += sourceSets.intTest.resources.srcDirs
            scopes.TEST.plus += [configurations.intTestImplementation]
        }
    }

    test {
        testLogging {
            exceptionFormat = 'full'
            events "PASSED", "SKIPPED", "FAILED"
        }
        // Plugin for retrying flaky tests. Reference: https://github.com/gradle/test-retry-gradle-plugin
        retry {
            // The maximum number of times to retry an individual test
            maxRetries = 3
            // The maximum number of test failures that are allowed (per module) before retrying is disabled. The count applies to
            // each round of test execution. For example, if maxFailures is 5 and 4 tests initially fail and then 3
            // again on retry, this will not be considered too many failures and retrying will continue (if maxRetries {@literal >} 1).
            // If 5 or more tests were to fail initially then no retry would be attempted.
            maxFailures = 10
            // Whether tests that initially fail and then pass on retry should fail the task.
            failOnPassedAfterRetry = false
        }
        // Run tests sequentially for better stability
        maxParallelForks = 1
        maxHeapSize = "6g"
        systemProperty 'io.netty.leakDetection.level', 'paranoid'
        systemProperty 'io.netty.allocator.tinyCacheSize', '0'
        systemProperty 'io.netty.allocator.smallCacheSize', '0'
        systemProperty 'io.netty.allocator.normalCacheSize', '0'
        systemProperty 'io.netty.allocator.maxCachedBufferCapacity', '0'
    }

    task intTest(type: Test) {
        description = 'Runs integration tests.'
        group = 'verification'
        testClassesDirs = sourceSets.intTest.output.classesDirs
        classpath = sourceSets.intTest.runtimeClasspath
        testLogging {
            exceptionFormat = 'full'
            events 'started', 'failed', 'passed', 'skipped'
            showStandardStreams = false
        }
        // Allow for retrying flaky integration tests.
        retry {
            // The maximum number of times to retry an individual test
            maxRetries = 3
            // The maximum number of test failures that are allowed (per module) before retrying is disabled. The count applies to
            // each round of test execution. For example, if maxFailures is 5 and 4 tests initially fail and then 3
            // again on retry, this will not be considered too many failures and retrying will continue (if maxRetries {@literal >} 1).
            // If 5 or more tests were to fail initially then no retry would be attempted.
            maxFailures = 10
            // Whether tests that initially fail and then pass on retry should fail the task.
            failOnPassedAfterRetry = false
        }
        maxHeapSize = "6g"
        systemProperty 'io.netty.leakDetection.level', 'paranoid'
    }

    // ByteBuf Flow Tracker JVM configuration for test tasks
    // The agent build tasks are defined at the root level below
    def byteBufTrackingEnabled = project.hasProperty('withByteBufTracking') ||
                                 project.hasProperty('enableByteBufTracking')

    if (byteBufTrackingEnabled) {
        def agentJar = file("${rootProject.projectDir}/modules/bytebuddy-bytebuf-tracer/bytebuf-flow-tracker/target/bytebuf-flow-tracker-1.0.0-SNAPSHOT-agent.jar")

        // Ensure the agent is built before tests run (reference root-level task)
        test.dependsOn rootProject.tasks.named('buildByteBufAgent')
        intTest.dependsOn rootProject.tasks.named('buildByteBufAgent')

        // Configure test task with ByteBuf tracker
        test {
            doFirst {
                logger.lifecycle("ByteBuf Flow Tracker ENABLED")
                logger.lifecycle("Using agent: ${agentJar}")
            }

            // Attach the Java agent with configuration
            // - include: Track all com.github.ambry packages
            // - exclude: Exclude specific packages/classes to avoid Mockito conflicts
            //   * org.mockito.* - Mockito framework package (use .* for packages)
            //   * MySqlAccountStore - Mocked in account service tests
            //   * AggregatedAccountReportsDaoTest - SQL tests with no ByteBuf usage
            //   * ServerRequestResponseHelper - Helper class causing instrumentation issues
            //   * FileCopy protocol classes - Some mocked, some don't use ByteBuf
            //     - FileCopyGetMetaDataResponse, FileCopyGetChunkResponse (use ByteBuf, mocked)
            //     - FileCopyDataVerificationRequest, FileCopyDataVerificationResponse (no ByteBuf)
            //     - FileCopyGetChunkRequest, FileCopyGetMetaDataRequest (no ByteBuf)
            //   * Protocol base classes and interfaces - Mockito conflicts in file transfer tests
            //     - SendWithCorrelationId, Send (network interfaces)
            //     - Response, RequestOrResponse, RequestTracker (protocol base classes/interfaces)
            //     - AbstractByteBufHolder (base wrapper class)
            //   * Netty interfaces - Mockito cannot mock instrumented classes implementing these
            //     - ReferenceCounted, ByteBufHolder (core Netty interfaces)
            // - trackConstructors: Track ByteBuf wrappers that store ByteBuf in constructors
            jvmArgs "-javaagent:${agentJar.absolutePath}=" +
                "include=com.github.ambry.*;" +
                "exclude=org.mockito.*,com.github.ambry.account.mysql.MySqlAccountStore,com.github.ambry.accountstats.AggregatedAccountReportsDaoTest,com.github.ambry.network.ServerRequestResponseHelper,com.github.ambry.protocol.FileCopyGetMetaDataResponse,com.github.ambry.protocol.FileCopyGetChunkResponse,com.github.ambry.protocol.FileCopyDataVerificationRequest,com.github.ambry.protocol.FileCopyDataVerificationResponse,com.github.ambry.protocol.FileCopyGetChunkRequest,com.github.ambry.protocol.FileCopyGetMetaDataRequest,com.github.ambry.network.SendWithCorrelationId,com.github.ambry.protocol.Response,com.github.ambry.protocol.RequestOrResponse,com.github.ambry.utils.AbstractByteBufHolder,com.github.ambry.network.Send,com.github.ambry.protocol.RequestTracker,io.netty.util.ReferenceCounted,io.netty.buffer.ByteBufHolder;" +
                "trackConstructors=" +
                    "com.github.ambry.protocol.*," +                                   // All protocol messages (PutRequest, GetRequest, etc)
                    "com.github.ambry.messageformat.BlobData," +                       // Blob data wrapper
                    "com.github.ambry.network.SocketServerRequest," +                  // Socket request wrapper
                    "com.github.ambry.network.NettyServerRequest," +                   // Netty request wrapper
                    "com.github.ambry.network.ResponseInfo," +                         // Network response wrapper
                    "com.github.ambry.router.EncryptJob," +                            // Encryption job
                    "com.github.ambry.router.EncryptJob\$EncryptJobResult," +          // Encryption result (inner class)
                    "com.github.ambry.router.DecryptJob," +                            // Decryption job
                    "com.github.ambry.rest.NettyResponseChannel\$Chunk," +             // Response chunk (inner class)
                    "com.github.ambry.utils.NettyByteBufDataInputStream"               // ByteBuf to InputStream wrapper

            // Register test listener to print tracker output
            systemProperty 'junit.listeners', 'com.github.ambry.testutils.ByteBufTrackerListener'
        }

        // Configure intTest task with ByteBuf tracker
        intTest {
            doFirst {
                logger.lifecycle("ByteBuf Flow Tracker ENABLED (integration tests)")
                logger.lifecycle("Using agent: ${agentJar}")
            }

            // Same configuration as test task
            jvmArgs "-javaagent:${agentJar.absolutePath}=" +
                "include=com.github.ambry.*;" +
                "exclude=org.mockito.*,com.github.ambry.account.mysql.MySqlAccountStore,com.github.ambry.accountstats.AggregatedAccountReportsDaoTest,com.github.ambry.network.ServerRequestResponseHelper,com.github.ambry.protocol.FileCopyGetMetaDataResponse,com.github.ambry.protocol.FileCopyGetChunkResponse,com.github.ambry.protocol.FileCopyDataVerificationRequest,com.github.ambry.protocol.FileCopyDataVerificationResponse,com.github.ambry.protocol.FileCopyGetChunkRequest,com.github.ambry.protocol.FileCopyGetMetaDataRequest,com.github.ambry.network.SendWithCorrelationId,com.github.ambry.protocol.Response,com.github.ambry.protocol.RequestOrResponse,com.github.ambry.utils.AbstractByteBufHolder,com.github.ambry.network.Send,com.github.ambry.protocol.RequestTracker,io.netty.util.ReferenceCounted,io.netty.buffer.ByteBufHolder;" +
                "trackConstructors=" +
                    "com.github.ambry.protocol.*," +                                   // All protocol messages (PutRequest, GetRequest, etc)
                    "com.github.ambry.messageformat.BlobData," +
                    "com.github.ambry.network.SocketServerRequest," +
                    "com.github.ambry.network.NettyServerRequest," +
                    "com.github.ambry.network.ResponseInfo," +
                    "com.github.ambry.router.EncryptJob," +
                    "com.github.ambry.router.EncryptJob\$EncryptJobResult," +
                    "com.github.ambry.router.DecryptJob," +
                    "com.github.ambry.rest.NettyResponseChannel\$Chunk," +
                    "com.github.ambry.utils.NettyByteBufDataInputStream"

            systemProperty 'junit.listeners', 'com.github.ambry.testutils.ByteBufTrackerListener'
        }
    } else {
        logger.info("ByteBuf Flow Tracker disabled. Enable with: -PwithByteBufTracking")
    }

    task allTest {
        description = 'Runs unit and integration tests'
        group = 'verification'
    }
    allTest.dependsOn test
    allTest.dependsOn intTest

    javadoc {
        // TODO audit and fix our javadocs so that we don't need this setting
        // This is mainly for cases where param/throws tags don't have descriptions
        // Previously, javadocs weren't being compiled, but now shipkit automatically enables this build step
        options.addStringOption('Xdoclint:none', '-quiet') // Suppress lint warnings
        failOnError = false
    }
}

project(':ambry-utils') {
    dependencies {
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        implementation "commons-codec:commons-codec:$commonsVersion"
        implementation "org.json:json:$jsonVersion"
        implementation "net.sf.jopt-simple:jopt-simple:$joptSimpleVersion"
        implementation "io.netty:netty-all:$nettyVersion"
        testImplementation project(":ambry-test-utils")
        testImplementation "io.netty:netty-transport-native-epoll:$nettyVersion"
        testImplementation "commons-io:commons-io:$commonsIoVersion"
    }
}

project(':ambry-test-utils') {
    // Ensure ByteBuf tracker is built before compiling test-utils
    // (needed for AmbryByteBufObjectHandler to compile against ObjectTrackerHandler interface)
    tasks.compileJava.dependsOn(rootProject.tasks.named('buildByteBufAgent'))

    dependencies {
        implementation "junit:junit:$junitVersion"
        implementation "org.mockito:mockito-core:2.+"
        implementation project(":ambry-api")
        implementation project(":ambry-utils")
        implementation project(":ambry-commons")
        implementation project(":ambry-clustermap")
        implementation project(":ambry-network")
        implementation project(":ambry-router")
        implementation project(":ambry-server")
        implementation project(":ambry-store")
        implementation project(":ambry-file-transfer")
        implementation project(":ambry-protocol")
        implementation project(":ambry-messageformat")
        implementation project(":ambry-replication")
        implementation project(":ambry-mysql")
        implementation "org.bouncycastle:bcpkix-jdk15on:$bouncycastleVersion"
        implementation "org.conscrypt:conscrypt-openjdk-uber:$conscryptVersion"
        implementation "org.json:json:$jsonVersion"

        // ByteBuf tracker agent JAR (needed for AmbryByteBufObjectHandler to implement ObjectTrackerHandler)
        // compileOnly because the actual implementation comes from -javaagent at runtime
        compileOnly files("${rootProject.projectDir}/modules/bytebuddy-bytebuf-tracer/bytebuf-flow-tracker/target/bytebuf-flow-tracker-1.0.0-SNAPSHOT-agent.jar")
        implementation "org.apache.helix:helix-core:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:helix-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:zookeeper-api:$helixVersion" exclude group: 'org.apache.helix'
        implementation("org.apache.hadoop:hadoop-common:$hadoopCommonVersion") {
            exclude group: "org.bouncycastle"
        }
    }
}

project(':ambry-api') {
    apply plugin: 'java-library'

    dependencies {
        implementation project(':ambry-utils')
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
        implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
        implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jacksonVersion"
        implementation "commons-codec:commons-codec:$commonsVersion"
        // These dependencies are exposed in public API method signatures
        api "io.netty:netty-all:$nettyVersion"
        api "org.json:json:$jsonVersion"
        testImplementation project(':ambry-clustermap')
        testImplementation project(':ambry-test-utils')
    }
}

project(':ambry-messageformat') {
    dependencies {
        implementation project(':ambry-api'),
                project(':ambry-utils')
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        testImplementation project(':ambry-test-utils')
        testImplementation project(':ambry-commons')
        testImplementation project(path: ':ambry-api', configuration: 'testArchives')
    }
}

project(':ambry-commons') {
    dependencies {
        implementation project(':ambry-api'),
                project(':ambry-messageformat'),
                project(':ambry-utils')

        // See https://github.com/facebook/zstd/releases for Zstd releases.  On Jan 2022, latest release is 1.5.2
        // See https://repo.maven.apache.org/maven2/com/github/luben/zstd-jni/ for library versions.
        implementation "com.github.luben:zstd-jni:$zstdVersion"

        // See https://github.com/lz4/lz4/releases for LZ4 releases.
        // There are JNI and Pure Java LZ4 provided at https://repo.maven.apache.org/maven2/org/lz4
        // The latest LZ4 library is 1.8.0 on 06/2021.
        implementation "org.lz4:lz4-java:$lz4Version"

        implementation "org.conscrypt:conscrypt-openjdk-uber:$conscryptVersion"
        implementation "io.netty:netty-all:$nettyVersion"
        implementation "io.netty:netty-tcnative-boringssl-static:$nettyTcNativeBoringSSLStatic_linux_x86_64"
        implementation "io.netty:netty-tcnative-boringssl-static:$nettyTcNativeBoringSSLStatic_osx_aarch_64"
        implementation "org.apache.helix:helix-core:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:helix-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:zookeeper-api:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:metadata-store-directory-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:metrics-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "com.github.ben-manes.caffeine:caffeine:$caffeineVersion"
        testImplementation project(':ambry-test-utils')
        testImplementation project(':ambry-clustermap')
        testImplementation project(path: ':ambry-clustermap', configuration: 'testArchives')
    }
}


project(':ambry-account') {
    // Disable parallel test execution - tests share MySQL database
    test {
        maxParallelForks = 1
    }

    dependencies {
        implementation project(':ambry-api'),
                project(':ambry-utils'),
                project(':ambry-commons'),
                project(':ambry-mysql')
        implementation "org.apache.helix:helix-core:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:helix-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:zookeeper-api:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:metadata-store-directory-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:metrics-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        implementation "org.json:json:$jsonVersion"
        testImplementation project(':ambry-test-utils')
        testImplementation project(':ambry-server')
        testImplementation project(path: ':ambry-clustermap', configuration: 'testArchives')
        testImplementation project(path: ':ambry-api', configuration: 'testArchives')
        testImplementation project(path: ':ambry-server', configuration: 'testArchives')
        testImplementation "com.google.jimfs:jimfs:$jimFsVersion"
    }
}

project(':ambry-clustermap') {
    dependencies {
        implementation project(':ambry-api'),
                project(':ambry-commons'),
                project(':ambry-utils')
        implementation "org.apache.helix:helix-core:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:helix-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:helix-lock:$helixLockVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:zookeeper-api:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:metadata-store-directory-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:metrics-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
        implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
        implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        implementation "org.json:json:$jsonVersion"
        testImplementation project(':ambry-commons')
        testImplementation project(':ambry-test-utils')
        testImplementation project(':ambry-mysql')
        testImplementation project(path: ':ambry-api', configuration: 'testArchives')
    }
}

project(':ambry-network') {
    // Disable parallel test execution - tests bind to network ports
    test {
        maxParallelForks = 1
    }

    dependencies {
        implementation project(':ambry-api'),
                project(':ambry-utils'),
                project(':ambry-commons'),
                project(':ambry-clustermap'),
                project(':ambry-protocol')
        implementation "io.netty:netty-all:$nettyVersion"
        implementation "io.netty:netty-tcnative-boringssl-static:$nettyTcNativeBoringSSLStatic_linux_x86_64"
        implementation "io.netty:netty-tcnative-boringssl-static:$nettyTcNativeBoringSSLStatic_osx_aarch_64"
        implementation "io.netty:netty-transport-native-epoll:$nettyVersion"
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        testImplementation project(':ambry-test-utils')
        testImplementation "org.conscrypt:conscrypt-openjdk-uber:$conscryptVersion"
        testImplementation "org.bouncycastle:bcpkix-jdk15on:$bouncycastleVersion"
    }
}

project (':ambry-prioritization') {
    dependencies{
        implementation project(':ambry-api')
        implementation project(':ambry-commons')
        implementation project(':ambry-store')
        implementation project(':ambry-clustermap')
        implementation project(':ambry-replication')
        implementation project(':ambry-utils')
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        testImplementation project(':ambry-test-utils')
        testImplementation "com.google.guava:guava:$guavaVersion"
    }
}

project (':ambry-file-transfer') {
    dependencies{
        implementation project(':ambry-api')
        implementation project(':ambry-commons')
        implementation project(':ambry-store')
        implementation project(':ambry-prioritization')
        implementation project(':ambry-clustermap')
        implementation project(':ambry-protocol')
        implementation project(':ambry-utils')
        implementation project(':ambry-network')
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        testImplementation project(':ambry-test-utils')
        testImplementation project(path: ':ambry-clustermap', configuration: 'testArchives')
    }
}

project(':ambry-server') {
    dependencies {
        implementation project(':ambry-api'),
                project(':ambry-cloud'),
                project(':ambry-clustermap'),
                project(':ambry-messageformat'),
                project(':ambry-network'),
                project(':ambry-protocol'),
                project(':ambry-commons'),
                project(':ambry-store'),
                project(':ambry-utils'),
                project(':ambry-rest'),
                project(':ambry-account'),
                project(':ambry-replication'),
                project(':ambry-mysql'),
                project(':ambry-prioritization'),
                project(':ambry-file-transfer')
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        implementation "io.dropwizard.metrics:metrics-jmx:$metricsVersion"
        implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        runtimeOnly "org.apache.logging.log4j:log4j-core:$log4jVersion"
        runtimeOnly "org.apache.logging.log4j:log4j-api:$log4jVersion"
        runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
        testImplementation project(':ambry-router')
        testImplementation project(':ambry-test-utils')
        testImplementation project(path: ':ambry-clustermap', configuration: 'testArchives')
        testImplementation project(path: ':ambry-api', configuration: 'testArchives')
        testImplementation project(path: ':ambry-replication', configuration: 'testArchives')
        testImplementation project(path: ':ambry-messageformat', configuration: 'testArchives')
        testImplementation project(path: ':ambry-store', configuration: 'testArchives')
    }
}

project(':ambry-store') {
    dependencies {
        implementation project(':ambry-api'),
                project(':ambry-utils'),
                project(':ambry-account'),
                project(':ambry-messageformat'),
                project(':ambry-commons'),
                project(':ambry-clustermap')
        implementation "net.smacke:jaydio:$jaydioVersion"
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
        testImplementation project(':ambry-test-utils')
        testImplementation project(path: ':ambry-clustermap', configuration: 'testArchives')
        testImplementation project(path: ':ambry-api', configuration: 'testArchives')
        testImplementation "commons-io:commons-io:$commonsIoVersion"
        testImplementation "org.apache.helix:helix-core:$helixVersion" exclude group: 'org.apache.helix'
        testImplementation "org.apache.helix:helix-common:$helixVersion" exclude group: 'org.apache.helix'
        testImplementation "org.apache.helix:zookeeper-api:$helixVersion" exclude group: 'org.apache.helix'
        testImplementation "org.apache.helix:metadata-store-directory-common:$helixVersion" exclude group: 'org.apache.helix'
        testImplementation "org.apache.helix:metrics-common:$helixVersion" exclude group: 'org.apache.helix'
        testImplementation("org.apache.hadoop:hadoop-common:$hadoopCommonVersion") {
            exclude group: "org.bouncycastle"
        }
    }

    test {
        // Fork a new JVM for each test class to ensure thread isolation
        // StorageManagerTest counts threads globally via Thread.getAllStackTraces()
        forkEvery = 1
        testLogging {
            exceptionFormat = 'full'
            events 'started', 'failed', 'passed', 'skipped'
            showStandardStreams = false
        }
    }
}


project(':ambry-replication') {
    dependencies {
        implementation project(':ambry-api'),
                project(':ambry-utils'),
                project(':ambry-commons'),
                project(':ambry-protocol'),
                project(':ambry-store'),
                project(':ambry-network'),
                project(':ambry-clustermap'),
                project(':ambry-messageformat')
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
        testImplementation project(':ambry-store')
        testImplementation project(':ambry-clustermap')
        testImplementation project(':ambry-test-utils')
        testImplementation project(path: ':ambry-clustermap', configuration: 'testArchives')
        testImplementation project(path: ':ambry-api', configuration: 'testArchives')
        testImplementation project(path: ':ambry-messageformat', configuration: 'testArchives')
        testImplementation project(path: ':ambry-store', configuration: 'testArchives')
    }
}

project(':ambry-tools') {
    dependencies {
        implementation project(':ambry-api'),
                project(':ambry-account'),
                project(':ambry-store'),
                project(':ambry-server'),
                project(':ambry-rest'),
                project(':ambry-frontend'),
                project(':ambry-cloud'),
                project(':ambry-named-mysql'),
                project(':ambry-test-utils'),
                project(':ambry-protocol'),
                project(':ambry-utils'),
                project(':ambry-commons'),
                project(':ambry-messageformat'),
                project(':ambry-network'),
                project(':ambry-mysql'),
                project(':ambry-clustermap'),
                project(':ambry-replication')
        implementation "io.dropwizard.metrics:metrics-jmx:$metricsVersion"
        implementation "net.sf.jopt-simple:jopt-simple:$joptSimpleVersion"
        implementation platform("com.azure:azure-sdk-bom:$azureSdkBom")
        implementation "com.azure:azure-storage-blob"
        implementation "com.azure:azure-storage-blob-batch"
        implementation "org.apache.helix:helix-core:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:helix-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:zookeeper-api:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:metadata-store-directory-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:metrics-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "commons-io:commons-io:$commonsIoVersion"
        implementation "org.bouncycastle:bcpkix-jdk15on:$bouncycastleVersion"
        implementation "com.zaxxer:HikariCP:$hikariVersion"
        testImplementation project(path: ':ambry-clustermap', configuration: 'testArchives')
        testImplementation project(path: ':ambry-account', configuration: 'testArchives')
        testImplementation project(path: ':ambry-store', configuration: 'testArchives')
        testImplementation project(path: ':ambry-api', configuration: 'testArchives')
    }
}

project(':ambry-protocol') {
    dependencies {
        implementation project(':ambry-api'),
                project(':ambry-clustermap'),
                project(':ambry-messageformat'),
                project(':ambry-utils'),
                project(':ambry-commons')
        testImplementation project(':ambry-test-utils')
        testImplementation "org.apache.commons:commons-math3:3.6.1"
        testImplementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    }
}

project(':ambry-rest') {
    dependencies {
        implementation project(':ambry-api'),
                project(':ambry-account'),
                project(':ambry-utils'),
                project(':ambry-commons'),
                project(':ambry-quota'),
                project(':ambry-network')
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        implementation "io.dropwizard.metrics:metrics-jmx:$metricsVersion"
        implementation "net.sf.jopt-simple:jopt-simple:$joptSimpleVersion"
        implementation "io.netty:netty-all:$nettyVersion"
        implementation "io.netty:netty-tcnative-boringssl-static:$nettyTcNativeBoringSSLStatic_linux_x86_64"
        implementation "io.netty:netty-tcnative-boringssl-static:$nettyTcNativeBoringSSLStatic_osx_aarch_64"
        implementation "io.netty:netty-transport-native-epoll:$nettyVersion"
        implementation "javax.servlet:javax.servlet-api:$javaxVersion"
        implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
        implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
        runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
        testImplementation project(':ambry-test-utils')
        testImplementation project(path: ':ambry-api', configuration: 'testArchives')
        testImplementation project(path: ':ambry-account', configuration: 'testArchives')
        testImplementation "org.bouncycastle:bcpkix-jdk15on:$bouncycastleVersion"
    }
}

project(':ambry-router') {
    dependencies {
        implementation project(':ambry-api'),
                project(':ambry-utils'),
                project(':ambry-commons'),
                project(':ambry-clustermap'),
                project(':ambry-messageformat'),
                project(':ambry-protocol'),
                project(':ambry-network'),
                project(':ambry-cloud'),
                project(':ambry-rest'),
                project(':ambry-quota')
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        implementation "org.bouncycastle:bcpkix-jdk15on:$bouncycastleVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        testImplementation project(':ambry-test-utils')
        testImplementation project(':ambry-mysql')
        testImplementation project(path: ':ambry-network', configuration: 'testArchives')
        testImplementation project(path: ':ambry-cloud', configuration: 'testArchives')
        testImplementation "org.apache.commons:commons-lang3:$commonsLangVersion"
        testImplementation "org.apache.commons:commons-math:2.2"
        testImplementation "org.apache.commons:commons-math3:3.6.1"
    }
}

project(':ambry-frontend') {
    dependencies {
        implementation project(':ambry-api'),
                project(':ambry-account'),
                project(':ambry-utils'),
                project(':ambry-commons'),
                project(':ambry-rest'),
                project(':ambry-router'),
                project(':ambry-quota'),
                project(':ambry-clustermap')
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jacksonVersion"
        implementation "commons-codec:commons-codec:$commonsVersion"
        testImplementation project(':ambry-test-utils')
        testImplementation project(':ambry-quota')
        testImplementation project(':ambry-named-mysql')
        testImplementation project(path: ':ambry-api', configuration: 'testArchives')
        testImplementation project(path: ':ambry-rest', configuration: 'testArchives')
        testImplementation "com.google.guava:guava:$guavaVersion"
        testImplementation "org.apache.commons:commons-lang3:$commonsLangVersion"
    }
}

project(':ambry-cloud') {
    // Disable parallel test execution - tests share Azurite instance
    test {
        maxParallelForks = 1
    }

    dependencies {
        implementation project(':ambry-api'),
            project(':ambry-utils'),
            project(':ambry-commons'),
            project(':ambry-account'),
            project(':ambry-replication'),
            project(':ambry-rest'),
            project(':ambry-store'),
            project(':ambry-messageformat'),
            project(':ambry-clustermap'),
            project(':ambry-protocol')

        // use compile to include these jars during compile-time and make it avlb for ambry-tools
        // https://mvnrepository.com/artifact/com.azure/azure-sdk-bom
        implementation platform("com.azure:azure-sdk-bom:$azureSdkBom")
        implementation "com.azure:azure-cosmos"
        implementation "com.azure:azure-data-tables"
        implementation "com.azure:azure-identity"
        implementation "com.azure:azure-security-keyvault-secrets"
        implementation "com.azure:azure-storage-blob"
        implementation "com.azure:azure-storage-blob-batch"

        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        implementation "io.dropwizard.metrics:metrics-jmx:$metricsVersion"
        implementation "com.microsoft.azure:msal4j:$azureMsal4jVersion"
        implementation "org.apache.helix:helix-core:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:helix-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:helix-lock:$helixLockVersion" exclude group: "org.apache.helix"
        implementation "org.apache.helix:zookeeper-api:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:metadata-store-directory-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:metrics-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.httpcomponents:httpclient:$httpclientVersion"
        implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
        testImplementation project(':ambry-router')
        testImplementation project(':ambry-test-utils')
        testImplementation project(path: ':ambry-clustermap', configuration: 'testArchives')
        testImplementation project(path: ':ambry-store', configuration: 'testArchives')
        testImplementation project(path: ':ambry-replication', configuration: 'testArchives')
    }
}

project(':ambry-vcr') {
    // Disable parallel test execution - tests share Azurite instance
    test {
        maxParallelForks = 1
    }

    dependencies {
        implementation project(':ambry-api'),
                project(':ambry-server'),
                project(':ambry-cloud'),
                project(':ambry-commons'),
                project(':ambry-utils'),
                project(':ambry-network'),
                project(':ambry-rest'),
                project(':ambry-protocol'),
                project(':ambry-replication'),
                project(':ambry-messageformat')
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        implementation "io.dropwizard.metrics:metrics-jmx:$metricsVersion"
        testImplementation project(':ambry-test-utils')
        testImplementation project(':ambry-clustermap')
        testImplementation project(':ambry-store')
        testImplementation project(path: ':ambry-clustermap', configuration: 'testArchives')
        testImplementation project(path: ':ambry-cloud', configuration: 'testArchives')
        testImplementation project(path: ':ambry-store', configuration: 'testArchives')
        testImplementation project(path: ':ambry-replication', configuration: 'testArchives')
        testImplementation platform("com.azure:azure-sdk-bom:$azureSdkBom")
        testImplementation "com.azure:azure-data-tables"
        testImplementation "com.azure:azure-storage-blob"
        testImplementation "org.apache.helix:helix-core:$helixVersion" exclude group: 'org.apache.helix'
        testImplementation "org.apache.helix:helix-common:$helixVersion" exclude group: 'org.apache.helix'
        testImplementation "org.apache.helix:zookeeper-api:$helixVersion" exclude group: 'org.apache.helix'
        testImplementation "org.apache.helix:metadata-store-directory-common:$helixVersion" exclude group: 'org.apache.helix'
        testImplementation "org.apache.helix:metrics-common:$helixVersion" exclude group: 'org.apache.helix'
        testImplementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        testImplementation "commons-io:commons-io:$commonsIoVersion"
        testImplementation "org.apache.httpcomponents:httpclient:$httpclientVersion"
    }
}

project(':ambry-named-mysql') {
    // Disable parallel test execution - tests share MySQL database
    test {
        maxParallelForks = 1
    }

    dependencies {
        implementation project(':ambry-api')
        implementation project(':ambry-mysql')
        implementation project(':ambry-commons')
        implementation project(':ambry-utils')
        implementation "mysql:mysql-connector-java:$mysqlConnectorVersion"
        implementation "com.zaxxer:HikariCP:$hikariVersion"
        implementation "io.dropwizard.metrics:metrics-core:$metricsVersion"
        implementation "commons-codec:commons-codec:$commonsVersion"
        testImplementation project(':ambry-test-utils')
    }
}

project(':ambry-quota') {
    dependencies {
        implementation project(':ambry-api'),
            project(':ambry-commons'),
            project(':ambry-mysql'),
            project(':ambry-clustermap'),
            project(':ambry-utils')
        implementation "org.apache.helix:helix-core:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:helix-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:zookeeper-api:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:metadata-store-directory-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:metrics-common:$helixVersion" exclude group: 'org.apache.helix'
        testImplementation project(':ambry-test-utils')
        testImplementation project(':ambry-account')
        testImplementation project(path: ':ambry-clustermap', configuration: 'testArchives')
    }
}

project(':ambry-mysql') {
    // Disable parallel test execution - tests share MySQL database
    test {
        maxParallelForks = 1
    }

    dependencies {
        implementation project(':ambry-api')
        implementation project(':ambry-utils')
        implementation project(':ambry-commons')
        implementation "mysql:mysql-connector-java:$mysqlConnectorVersion"
        implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
        implementation "org.apache.helix:helix-core:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:helix-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:zookeeper-api:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:metadata-store-directory-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "org.apache.helix:metrics-common:$helixVersion" exclude group: 'org.apache.helix'
        implementation "com.zaxxer:HikariCP:$hikariVersion"
        testImplementation project(':ambry-test-utils')
        testImplementation project(path: ':ambry-clustermap', configuration: 'testArchives')
        testImplementation project(path: ':ambry-api', configuration: 'testArchives')
    }
}

project(':ambry-all') {
    dependencies {
        // this is a meta project that depends on all of the "entry-point" subprojects to make it easier to pull in the
        // entire dependency tree.
        implementation project(':ambry-frontend')
        implementation project(':ambry-server')
        implementation project(':ambry-tools')
        implementation project(':ambry-cloud')
        implementation project(':ambry-named-mysql')
        implementation project(':ambry-test-utils')
        implementation project(':ambry-quota')
        implementation project(':ambry-vcr')
        implementation project(':ambry-file-transfer')
    }
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path)}

task allJar(type: Jar, dependsOn: subprojects.assemble) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Implementation-Title': 'Ambry',
                   'Main-Class': 'com.github.ambry.server.AmbryMain'
    }
    zip64 true
    destinationDirectory.set(file('target'))
    archiveBaseName.set('ambry')
    subprojects.each { subproject ->
        from  {
            (subproject.configurations.archives.allArtifacts.files + subproject.configurations.runtimeClasspath).collect {
                zipTree(it)
            }
        }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

task allJarVcr(type: Jar, dependsOn: subprojects.assemble) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Implementation-Title': 'Vcr',
                   'Main-Class': 'com.github.ambry.vcr.VcrMain'
    }
    zip64 true
    destinationDirectory.set(file('target'))
    archiveBaseName.set('ambry-vcr')
    subprojects.each { subproject ->
        from  {
            (subproject.configurations.archives.allArtifacts.files + subproject.configurations.runtimeClasspath).collect {
                zipTree(it)
            }
        }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

task codeCoverageReport(type: JacocoReport) {
    // This task will compile a report for whatever tests have been run.
    // Make sure to run the desired test tasks before generating the coverate report.
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    // always rerun task when requested to get new execution data
    outputs.upToDateWhen { false }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled true
        csv.enabled true
    }
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}
